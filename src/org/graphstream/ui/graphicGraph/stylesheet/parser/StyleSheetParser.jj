options { JDK_VERSION = "1.5"; STATIC = false; }

PARSER_BEGIN(StyleSheetParser)
/*
 * Copyright 2006 - 2012
 *     Stefan Balev     <stefan.balev@graphstream-project.org>
 *     Julien Baudry	<julien.baudry@graphstream-project.org>
 *     Antoine Dutot	<antoine.dutot@graphstream-project.org>
 *     Yoann Pign√©		<yoann.pigne@graphstream-project.org>
 *     Guilhelm Savin	<guilhelm.savin@graphstream-project.org>
 * 
 * This file is part of GraphStream <http://graphstream-project.org>.
 * 
 * GraphStream is a library whose purpose is to handle static or dynamic
 * graph, create them from scratch, file or any source and display them.
 * 
 * This program is free software distributed under the terms of two licenses, the
 * CeCILL-C license that fits European law, and the GNU Lesser General Public
 * License. You can  use, modify and/ or redistribute the software under the terms
 * of the CeCILL-C license as circulated by CEA, CNRS and INRIA at the following
 * URL <http://www.cecill.info> or under the terms of the GNU LGPL as published by
 * the Free Software Foundation, either version 3 of the License, or (at your
 * option) any later version.
 * 
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
 * PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 * 
 * The fact that you are presently reading this means that you have had
 * knowledge of the CeCILL-C and LGPL licenses and that you accept their terms.
 */
package org.graphstream.ui.graphicGraph.stylesheet.parser;

import java.awt.Color;

import java.io.InputStream;
import java.io.IOException;
import java.io.Reader;

import org.graphstream.ui.graphicGraph.stylesheet.*;

import org.graphstream.util.parser.ParseException;
import org.graphstream.util.parser.SimpleCharStream;
import org.graphstream.util.parser.Token;
import org.graphstream.util.parser.TokenMgrError;

@SuppressWarnings("unused")
public class StyleSheetParser {
	/**
	 * The style sheet.
	 */
	protected StyleSheet stylesheet;
	
	public StyleSheetParser( StyleSheet stylesheet, InputStream stream )
	{
		this( stream );
		this.stylesheet = stylesheet;
	}
	
	public StyleSheetParser( StyleSheet stylesheet, Reader stream )
	{
		this( stream );
		this.stylesheet = stylesheet;
	}
	
	public static class Number
	{
		public float value;
		public Style.Units units = Style.Units.PX;
		public Number( float value, Style.Units units ) { this.value=value; this.units = units; }
	}
	
	/**
	 * Closes the parser, closing the opened stream.
	 */
    public void close() throws IOException {
		jj_input_stream.close();
	}
}
PARSER_END(StyleSheetParser)

/*
 * The lexer.
 */

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
}
//
// Base tokens.
//
TOKEN: { < #EOL              : (("\r")|("\n"))> }
TOKEN: { < #DIGIT            : ["0"-"9"] > }
TOKEN: { < #HEXDIGIT         : (["0"-"9","a"-"f","A"-"F"])> }
TOKEN: { < #UNITS            : (("gu")|("px")|("%"))> }
TOKEN: { < DOT               : "." > }
TOKEN: { < LBRACE            : "{" > }
TOKEN: { < RBRACE            : "}" > }
TOKEN: { < LPAREN            : "(" > }
TOKEN: { < RPAREN            : ")" > }
TOKEN: { < SHARP             : "#" > }
TOKEN: { < COLON             : ":" > }
TOKEN: { < SEMICOLON         : ";" > }
TOKEN: { < COMA              : "," > }
//TOKEN: { < RGBA              : "rgba("(<DIGIT>){1,3}","(<DIGIT>){1,3}","(<DIGIT>){1,3}","(<DIGIT>){1,3}")" > }
//TOKEN: { < RGB               : "rgb("(<DIGIT>){1,3}","(<DIGIT>){1,3}","(<DIGIT>){1,3}")" > }
TOKEN: { < RGBA              : "rgba" > }
TOKEN: { < RGB               : "rgb" > } 
TOKEN: { < HTMLCOLOR         : ( "#" (((<HEXDIGIT>){3})|((<HEXDIGIT>){4})|((<HEXDIGIT>){6})|((<HEXDIGIT>){8})|((<HEXDIGIT>){9})|((<HEXDIGIT>){12})|((<HEXDIGIT>){16})) ) > }
TOKEN: { < REAL              : ( "-" | "+" )? ( <DIGIT> )+ ( "." (<DIGIT>)+ )? (<UNITS>)? > } 
TOKEN: { < STRING            : (("\"" (~["\""])* "\"")|("'" (~["'"])* "'")) > }
TOKEN: { < URL               : "url" > }
//
// Selectors.
//
TOKEN: { < GRAPH             : "graph" > }
TOKEN: { < EDGE              : "edge" > }
TOKEN: { < NODE              : "node" > }
TOKEN: { < SPRITE            : "sprite" > }
//
// General properties.
//
TOKEN: { < FILLMODE          : "fill-mode" > }
TOKEN: { < FILLCOLOR         : "fill-color" > }
TOKEN: { < FILLIMAGE         : "fill-image" > }

TOKEN: { < STROKEMODE        : "stroke-mode" > }
TOKEN: { < STROKECOLOR       : "stroke-color" > } 	 
TOKEN: { < STROKEWIDTH       : "stroke-width" > }

TOKEN: { < SHADOWMODE        : "shadow-mode" > }
TOKEN: { < SHADOWCOLOR       : "shadow-color" > }
TOKEN: { < SHADOWWIDTH       : "shadow-width" > }
TOKEN: { < SHADOWOFFSET      : "shadow-offset" > }

TOKEN: { < TEXTMODE          : "text-mode" > }
TOKEN: { < TEXTCOLOR         : "text-color" > }
TOKEN: { < TEXTSTYLE         : "text-style" > }
TOKEN: { < TEXTFONT          : "text-font" > }
TOKEN: { < TEXTSIZE          : "text-size" > }
TOKEN: { < TEXTVISIBILITYMODE: "text-visibility-mode" > }
TOKEN: { < TEXTVISIBILITY    : "text-visibility" > }
TOKEN: { < TEXTBACKGROUNDMODE: "text-background-mode" > }
TOKEN: { < TEXTBACKGROUNDCOLOR: "text-background-color" > }
TOKEN: { < TEXTOFFSET        : "text-offset" > }
TOKEN: { < TEXTPADDING       : "text-padding" > }

TOKEN: { < ICONMODE          : "icon-mode" > }
TOKEN: { < ICON              : "icon" > }

TOKEN: { < PADDING           : "padding" > }

TOKEN: { < ZINDEX            : "z-index" > }
TOKEN: { < VISIBILITYMODE    : "visibility-mode" > }
TOKEN: { < VISIBILITY        : "visibility" > }
//
// Properties common to nodes, edges and sprites.
//
TOKEN: { < SHAPE             : "shape" > }
TOKEN: { < SIZE              : "size" > }
TOKEN: { < SIZEMODE          : "size-mode" > }
TOKEN: { < SHAPEPOINTS       : "shape-points" > }
TOKEN: { < TEXTALIGNMENT     : "text-alignment" > }
TOKEN: { < JCOMPONENT        : "jcomponent" > }
//
// Properties for edges only.
//
TOKEN: { < ARROWIMGURL       : "arrow-image" > }
TOKEN: { < ARROWSIZE         : "arrow-size" > }
TOKEN: { < ARROWSHAPE        : "arrow-shape" > }
//
// Properties for sprites only.
//
TOKEN: { < SPRITEORIENT      : "sprite-orientation" > }
//
// Properties for graphs only.
//
TOKEN: { < CANVASCOLOR       : "canvas-color" > }
//
// Property values.
//
TOKEN: { < PLAIN             : "plain" > }
TOKEN: { < DYNPLAIN          : "dyn-plain" > }
TOKEN: { < DYNSIZE           : "dyn-size" > }
TOKEN: { < DYNICON           : "dyn-icon" > }

TOKEN: { < DASHES            : "dashes" > }
TOKEN: { < DOTS              : "dots" > }
TOKEN: { < DOUBLE            : "double" > }

TOKEN: { < GRADIENTDIAGONAL1 : "gradient-diagonal1" > }
TOKEN: { < GRADIENTDIAGONAL2 : "gradient-diagonal2" > }
TOKEN: { < GRADIENTHORIZONTAL: "gradient-horizontal" > }
TOKEN: { < GRADIENTRADIAL    : "gradient-radial" > }
TOKEN: { < GRADIENTVERTICAL  : "gradient-vertical" > }

TOKEN: { < HIDDEN            : "hidden" > }

TOKEN: { < IMAGETILED        : "image-tiled" > }
TOKEN: { < IMAGESCALED       : "image-scaled" > }
TOKEN: { < IMAGESCALEDRATIOMAX : "image-scaled-ratio-max" > }
TOKEN: { < IMAGESCALEDRATIOMIN : "image-scaled-ratio-min" > }

TOKEN: { < NONE              : "none" > }
TOKEN: { < NORMAL            : "normal" > }
TOKEN: { < TRUNCATED         : "truncated"> }
TOKEN: { < ZOOMRANGE         : "zoom-range" > }
TOKEN: { < ATZOOM            : "at-zoom" >  }
TOKEN: { < UNDERZOOM         : "under-zoom" > }
TOKEN: { < OVERZOOM          : "over-zoom" > }
TOKEN: { < ZOOMS             : "zooms" > }
TOKEN: { < FIT               : "fit" >  }

TOKEN: { < BOLD              : "bold" > }
TOKEN: { < BOLD_ITALIC       : "bold-italic" > }
TOKEN: { < ITALIC            : "italic" > }

TOKEN: { < ALONG             : "along" > }
TOKEN: { < ATLEFT            : "at-left" > }
TOKEN: { < ATRIGHT           : "at-right" > }
TOKEN: { < CENTER            : "center" > }
TOKEN: { < LEFT              : "left" > }
TOKEN: { < RIGHT             : "right" > }
TOKEN: { < UNDER             : "under" > }
TOKEN: { < ABOVE             : "above" > }
TOKEN: { < JUSTIFY           : "justify" > }

TOKEN: { < CIRCLE            : "circle" > }
TOKEN: { < TRIANGLE          : "triangle" > }
TOKEN: { < FREEPLANE         : "freeplane" > }
TOKEN: { < TEXTBOX           : "text-box" > }
TOKEN: { < TEXTROUNDEDBOX    : "text-rounded-box" > }
TOKEN: { < TEXTCIRCLE        : "text-circle" > }
TOKEN: { < TEXTDIAMOND       : "text-diamond" > }
TOKEN: { < TEXTPARAGRAPH     : "text-paragraph" > }
TOKEN: { < BOX               : "box" > }
TOKEN: { < ROUNDEDBOX        : "rounded-box" > }
TOKEN: { < CROSS             : "cross" > }
TOKEN: { < DIAMOND           : "diamond" > }
TOKEN: { < POLYGON           : "polygon" > }

TOKEN: { < BUTTON            : "button" > }
TOKEN: { < TEXTFIELD         : "text-field" > }
TOKEN: { < PANEL             : "panel" > }

TOKEN: { < LINE              : "line" > }
TOKEN: { < POLYLINE          : "polyline" > } 
TOKEN: { < POLYLINESCALED    : "polyline-scaled" > }
TOKEN: { < ANGLE             : "angle" > }
TOKEN: { < CUBICCURVE        : "cubic-curve" > }
TOKEN: { < BLOB              : "blob" > }
TOKEN: { < SQUARELINE        : "square-line" > }
TOKEN: { < LSQUARELINE       : "L-square-line" > }
TOKEN: { < HSQUARELINE       : "horizontal-square-line" > }
TOKEN: { < VSQUARELINE       : "vertical-square-line" > }

TOKEN: { < ARROW             : "arrow" > }
TOKEN: { < FLOW              : "flow" > }
TOKEN: { < PIECHART          : "pie-chart" > }
TOKEN: { < IMAGE             : "image" > }
TOKEN: { < IMAGES            : "images" > }

TOKEN: { < FROM              : "from" > }
TOKEN: { < NODE0             : "node0" > }
TOKEN: { < TO                : "to" > }
TOKEN: { < NODE1             : "node1" > }
TOKEN: { < PROJECTION        : "projection" > }

TOKEN: { < CLICKED           : "clicked" > }
TOKEN: { < SELECTED          : "selected" > }

//
// More base tokens that must appear at the end for precedence reasons.
//
TOKEN: { < IDENTIFIER    : (["a"-"z","A"-"Z"])(["a"-"z","A"-"Z","0"-"9","_"])* > }
TOKEN: { < COMMENT       : ("/*"(~["*"])* "*/" ) | ( "//"((~["\r","\n"])*)<EOL> ) > }


/*
 * The parser.
 */

void start():
{
	Rule r;
}
{
	( r=rule() { stylesheet.addRule( r ); }
	| <COMMENT>
	)* <EOF>
}

Rule rule():
{
	Selector select;
	Style    style;
	Rule     rule;
}
{
	select=select()
	{
		rule  = new Rule( select );
		style = new Style();
		rule.setStyle( style );
	}
	<LBRACE> ( styles( style ) ) <RBRACE>
	{ return rule; }
}

void styles( Style style ):
{}
{
	( style(style) )*
}

void stylesStart(Style style):
{}
{
	(style(style))* <EOF>
}

Selector select():
{
	Token    t;
	Selector select;
}
{
	( ( <GRAPH>  { select=new Selector( Selector.Type.GRAPH ); } )
	| ( <NODE>   { select=new Selector( Selector.Type.NODE ); } )
	| ( <EDGE>   { select=new Selector( Selector.Type.EDGE ); } )
	| ( <SPRITE> { select=new Selector( Selector.Type.SPRITE ); } )
	)
	( ( <SHARP> ( ( t=<IDENTIFIER> { select.setId( t.image ); } )
				| ( t=<STRING>     { select.setId( t.image.substring( 1, t.image.length()-1 ) ); } )
				| ( t=<REAL>       { select.setId( t.image.toString() ); } )
				)
	  )
	| ( <DOT>   ( ( t=<IDENTIFIER> { select.setClass( t.image ); } )
	            | ( t=<CLICKED>    { select.setClass( "clicked" ); } )  // If we want "clicked" and
	            | ( t=<SELECTED>   { select.setClass( "selected" ); } ) // "selected" to be normal classes.
				| ( t=<STRING>     { select.setClass( t.image.substring( 1, t.image.length()-1 ) ); } )
				)
	  )
	)?
	( <COLON> ( (   <CLICKED>    { select.setPseudoClass( "clicked"  ); } )  
	          | (   <SELECTED>   { select.setPseudoClass( "selected" ); } )
	          | ( t=<STRING>     { select.setPseudoClass( t.image.substring( 1, t.image.length()-1 ) ); } )
	          | ( t=<IDENTIFIER> { select.setPseudoClass( t.image    ); } )
	          )
	)?
	{ return select; }
}

void style( Style style ):
{
	Color                    color;
	Colors                   colors;
	String                   url;
	Value                    value;
	Values                   values;
	Style.FillMode           fillMode;
	Style.StrokeMode         strokeMode;
	Style.ShadowMode         shadowMode;
	Style.TextMode           textMode;
	Style.TextVisibilityMode textVisMode;
	Style.TextBackgroundMode textBgMode;
	Style.TextStyle          textStyle;
	Style.TextAlignment      textAlignment;
	Style.IconMode           iconMode;
	Style.VisibilityMode     visMode;
	Style.SizeMode           sizeMode;
	Style.Shape              shape;
	Style.SpriteOrientation  spriteOrient;
	Style.ArrowShape         arrowShape;
	Style.JComponents        component;
}
{
	( ( <COMMENT> )
	| ( <ZINDEX>             <COLON> value         = value()             <SEMICOLON> { style.setValue( "z-index", new Integer( (int)value.value )  ); } )
	| ( <FILLMODE>           <COLON> fillMode      = fillMode()          <SEMICOLON> { style.setValue( "fill-mode",            fillMode       ); } )
	| ( <FILLCOLOR>          <COLON> colors        = colors()            <SEMICOLON> { style.setValue( "fill-color",           colors         ); } )
	| ( <FILLIMAGE>          <COLON> url           = url()               <SEMICOLON> { style.setValue( "fill-image",           url            ); } )
	| ( <STROKEMODE>         <COLON> strokeMode    = strokeMode()        <SEMICOLON> { style.setValue( "stroke-mode",          strokeMode     ); } )
	| ( <STROKECOLOR>        <COLON> colors        = colors()            <SEMICOLON> { style.setValue( "stroke-color",         colors         ); } )
	| ( <STROKEWIDTH>        <COLON> value         = value()             <SEMICOLON> { style.setValue( "stroke-width",         value          ); } )
	| ( <SHADOWMODE>         <COLON> shadowMode    = shadowMode()        <SEMICOLON> { style.setValue( "shadow-mode",          shadowMode     ); } )
	| ( <SHADOWCOLOR>        <COLON> colors        = colors()            <SEMICOLON> { style.setValue( "shadow-color",         colors         ); } )
	| ( <SHADOWWIDTH>        <COLON> value         = value()             <SEMICOLON> { style.setValue( "shadow-width",         value          ); } )
	| ( <SHADOWOFFSET>       <COLON> values        = values()            <SEMICOLON> { style.setValue( "shadow-offset",        values         ); } )
	| ( <PADDING>            <COLON> values        = values()            <SEMICOLON> { style.setValue( "padding",              values         ); } )
	| ( <TEXTMODE>           <COLON> textMode      = textMode()          <SEMICOLON> { style.setValue( "text-mode",            textMode       ); } )
	| ( <TEXTVISIBILITYMODE> <COLON> textVisMode   = textVisMode()       <SEMICOLON> { style.setValue( "text-visibility-mode", textVisMode    ); } )
	| ( <TEXTVISIBILITY>     <COLON> values        = values()            <SEMICOLON> { style.setValue( "text-visibility",      values         ); } )
	| ( <TEXTBACKGROUNDMODE> <COLON> textBgMode    = textBgMode()        <SEMICOLON> { style.setValue( "text-background-mode", textBgMode     ); } )
	| ( <TEXTCOLOR>          <COLON> colors        = colors()            <SEMICOLON> { style.setValue( "text-color",           colors         ); } )
	| ( <TEXTBACKGROUNDCOLOR><COLON> colors        = colors()            <SEMICOLON> { style.setValue( "text-background-color", colors        ); } )
	| ( <TEXTSTYLE>          <COLON> textStyle     = textStyle()         <SEMICOLON> { style.setValue( "text-style",           textStyle      ); } )
	| ( <TEXTFONT>           <COLON> url           = font()              <SEMICOLON> { style.setValue( "text-font",            url            ); } )
	| ( <TEXTSIZE>           <COLON> value         = value()             <SEMICOLON> { style.setValue( "text-size",            value          ); } )
	| ( <TEXTALIGNMENT>      <COLON> textAlignment = textAlign()         <SEMICOLON> { style.setValue( "text-alignment",       textAlignment  ); } )
	| ( <TEXTOFFSET>         <COLON> values        = values()            <SEMICOLON> { style.setValue( "text-offset",          values         ); } )
	| ( <TEXTPADDING>        <COLON> values        = values()            <SEMICOLON> { style.setValue( "text-padding",         values         ); } )
	| ( <ICONMODE>           <COLON> iconMode      = iconMode()          <SEMICOLON> { style.setValue( "icon-mode",            iconMode       ); } )
	| ( <ICON>               <COLON> url           = icon()              <SEMICOLON> { style.setValue( "icon",                 url            ); } )
	| ( <JCOMPONENT>         <COLON> component     = jcomponent()        <SEMICOLON> { style.setValue( "jcomponent",           component      ); } )
	| ( <VISIBILITYMODE>     <COLON> visMode       = visMode()           <SEMICOLON> { style.setValue( "visibility-mode",      visMode        ); } )
	| ( <VISIBILITY>         <COLON> values        = values()            <SEMICOLON> { style.setValue( "visibility",           values         ); } )
	| ( <SIZEMODE>           <COLON> sizeMode      = sizeMode()          <SEMICOLON> { style.setValue( "size-mode",            sizeMode       ); } )
	| ( <SIZE>               <COLON> values        = values()            <SEMICOLON> { style.setValue( "size",                 values         ); } )
	| ( <SHAPEPOINTS>        <COLON> values        = values()            <SEMICOLON> { style.setValue( "shape-points",         values         ); } )
	| ( <SHAPE>              <COLON> shape         = shape()             <SEMICOLON> { style.setValue( "shape",                shape          ); } )
	| ( <SPRITEORIENT>       <COLON> spriteOrient  = spriteOrient()      <SEMICOLON> { style.setValue( "sprite-orientation",   spriteOrient   ); } )
	| ( <ARROWSHAPE>         <COLON> arrowShape    = arrowShape()        <SEMICOLON> { style.setValue( "arrow-shape",          arrowShape     ); } )
	| ( <ARROWIMGURL>        <COLON> url           = url()               <SEMICOLON> { style.setValue( "arrow-image",          url            ); } )
	| ( <ARROWSIZE>          <COLON> values        = values()            <SEMICOLON> { style.setValue( "arrow-size",           values         ); } )	
	| ( <CANVASCOLOR>        <COLON> colors        = colors()            <SEMICOLON> { style.setValue( "canvas-color",         colors         ); } )
	)
}

Value value():
{
	Token       t;
	Style.Units units = Style.Units.PX;
	Value       value = null;
}
{
	t = <REAL> 
	{
		String nb = t.image.toLowerCase();
		
		if( nb.endsWith( "px" ) )
		{
			units = Style.Units.PX;
			nb    = nb.substring( 0, nb.length()-2 );
		}
		else if( nb.endsWith( "gu" ) )
		{
			units = Style.Units.GU;
			nb    = nb.substring( 0, nb.length()-2 ); 
		}
		else if( nb.endsWith( "%" ) )
		{
			units = Style.Units.PERCENTS;
			nb    = nb.substring( 0, nb.length()-1 );
		} 
	
		try{ value = new Value( units, Float.parseFloat( nb ) ); }
		catch( NumberFormatException e ) { generateParseException(); }
		return value;
	}
}

Values values():
{
	Values values;
	Value  value;
}
{
	         value = value() { values = new Values( value.units, value.value ); }
	( <COMA> value = value() { values.addValues( value.value ); } )*
	{ return values; }
}

String url():
{
	Token t;
}
{
	<URL> <LPAREN> t=<STRING> <RPAREN>
	{ return t.image.substring( 1, t.image.length()-1 ); }
}

String icon():
{
  String s;
}
{
	( <DYNICON > { return "dynamic"; }
	| s = url()   { return s; }
	) 
}

String font():
{
	Token  t;
	String s;
}
{
	( ( t=<IDENTIFIER> { s = t.image; } )
	| ( t=<STRING>     { s = t.image.substring( 1, t.image.length()-1 ); } )
	)
	{ return s; }
}

Color color():
{
	Token  t;
	String s;
	Token r, g, b, a;
}
{
	( ( <RGB>  <LPAREN> r=<REAL> <COMA> g=<REAL> <COMA> b=<REAL> <RPAREN> { s = String.format("rgb(%s,%s,%s)", r.image, g.image, b.image); } )
	| ( <RGBA> <LPAREN> r=<REAL> <COMA> g=<REAL> <COMA> b=<REAL> <COMA> a=<REAL> <RPAREN> { s = String.format("rgba(%s,%s,%s,%s)", r.image, g.image, b.image, a.image); } )
	| ( t=<HTMLCOLOR>  { s = t.image; } )
	| ( t=<IDENTIFIER> { s = t.image; } )
	| ( t=<STRING>     { s = t.image.substring( 1, t.image.length()-1 ); } )
	)
	{
		Color color = Style.convertColor( s );
		if( color == null ) color = Color.BLACK; 
		return color;
	}
}

Colors colors():
{
	Colors colors = new Colors();
	Color  color;
}
{
	         color = color() { colors.add( color ); }
	( <COMA> color = color() { colors.add( color ); } )*
	{ return colors; }
}

Style.FillMode fillMode():
{
	Style.FillMode m;
}
{
	( ( <NONE>                { m = Style.FillMode.NONE;                   } )
	| ( <PLAIN>               { m = Style.FillMode.PLAIN;                  } )
	| ( <DYNPLAIN>            { m = Style.FillMode.DYN_PLAIN;              } )
	| ( <GRADIENTRADIAL>      { m = Style.FillMode.GRADIENT_RADIAL;        } )
	| ( <GRADIENTVERTICAL>    { m = Style.FillMode.GRADIENT_VERTICAL;      } )
	| ( <GRADIENTHORIZONTAL>  { m = Style.FillMode.GRADIENT_HORIZONTAL;    } )
	| ( <GRADIENTDIAGONAL1>   { m = Style.FillMode.GRADIENT_DIAGONAL1;     } )
	| ( <GRADIENTDIAGONAL2>   { m = Style.FillMode.GRADIENT_DIAGONAL2;     } )
	| ( <IMAGETILED>          { m = Style.FillMode.IMAGE_TILED;            } )
	| ( <IMAGESCALED>         { m = Style.FillMode.IMAGE_SCALED;           } )
	| ( <IMAGESCALEDRATIOMAX> { m = Style.FillMode.IMAGE_SCALED_RATIO_MAX; } )
	| ( <IMAGESCALEDRATIOMIN> { m = Style.FillMode.IMAGE_SCALED_RATIO_MIN; } )
	)
	{ return m; }	
}

Style.StrokeMode strokeMode():
{
	Style.StrokeMode m;
}
{
	( ( <NONE>               { m = Style.StrokeMode.NONE;   } )
	| ( <PLAIN>              { m = Style.StrokeMode.PLAIN;  } )
	| ( <DASHES>             { m = Style.StrokeMode.DASHES; } )
	| ( <DOTS>               { m = Style.StrokeMode.DOTS;   } )
	| ( <DOUBLE>             { m = Style.StrokeMode.DOUBLE; } )
	)
	{ return m; }
}

Style.ShadowMode shadowMode():
{
	Style.ShadowMode s;
}
{
	( ( <NONE>                { s = Style.ShadowMode.NONE;                } )
	| ( <PLAIN>               { s = Style.ShadowMode.PLAIN;               } )
	| ( <GRADIENTRADIAL>      { s = Style.ShadowMode.GRADIENT_RADIAL;     } )
	| ( <GRADIENTHORIZONTAL>  { s = Style.ShadowMode.GRADIENT_HORIZONTAL; } )
	| ( <GRADIENTVERTICAL>    { s = Style.ShadowMode.GRADIENT_VERTICAL;   } )
	| ( <GRADIENTDIAGONAL1>   { s = Style.ShadowMode.GRADIENT_DIAGONAL1;  } )
	| ( <GRADIENTDIAGONAL2>   { s = Style.ShadowMode.GRADIENT_DIAGONAL2;  } )
	)
	{ return s; }
}

Style.TextMode textMode():
{
	Style.TextMode m;
}
{
	( ( <NORMAL>    { m = Style.TextMode.NORMAL;    } )
	| ( <HIDDEN>    { m = Style.TextMode.HIDDEN;    } )
	| ( <TRUNCATED> { m = Style.TextMode.TRUNCATED; } )
	)
	{ return m; }
}

Style.TextVisibilityMode textVisMode():
{
	Style.TextVisibilityMode m;
}
{
	( ( <NORMAL>    { m = Style.TextVisibilityMode.NORMAL;     } )
	| ( <HIDDEN>    { m = Style.TextVisibilityMode.HIDDEN;     } )
	| ( <ATZOOM>    { m = Style.TextVisibilityMode.AT_ZOOM;    } )
	| ( <UNDERZOOM> { m = Style.TextVisibilityMode.UNDER_ZOOM; } )
	| ( <OVERZOOM>  { m = Style.TextVisibilityMode.OVER_ZOOM;  } )
	| ( <ZOOMRANGE> { m = Style.TextVisibilityMode.ZOOM_RANGE; } )
	| ( <ZOOMS>     { m = Style.TextVisibilityMode.ZOOMS;      } )
	)
	{ return m; }
}

Style.TextBackgroundMode textBgMode():
{
  	Style.TextBackgroundMode m;
}
{
	( ( <NONE>       { m = Style.TextBackgroundMode.NONE;       } )
	| ( <PLAIN>      { m = Style.TextBackgroundMode.PLAIN;      } )
	| ( <ROUNDEDBOX> { m = Style.TextBackgroundMode.ROUNDEDBOX; } )
	)
	{ return m; } 
}

Style.TextStyle textStyle():
{
	Style.TextStyle t;
}
{
	( ( <NORMAL>      { t = Style.TextStyle.NORMAL;      } )
	| ( <BOLD>        { t = Style.TextStyle.BOLD;        } )
	| ( <ITALIC>      { t = Style.TextStyle.ITALIC;      } )
	| ( <BOLD_ITALIC> { t = Style.TextStyle.BOLD_ITALIC; } )
	)
	{ return t; }
}

Style.SizeMode sizeMode():
{
	Style.SizeMode m;
}
{
	( ( <NORMAL>  { m = Style.SizeMode.NORMAL;   } )
    | ( <FIT >    { m = Style.SizeMode.FIT;      } ) 
	| ( <DYNSIZE> { m = Style.SizeMode.DYN_SIZE; } )
	)
	{ return m; }
}

Style.TextAlignment textAlign():
{
	Style.TextAlignment t;
}
{
	( ( <CENTER>   { t = Style.TextAlignment.CENTER;    } )
	| ( <LEFT>     { t = Style.TextAlignment.LEFT;      } )
	| ( <RIGHT>    { t = Style.TextAlignment.RIGHT;     } )
	| ( <ATLEFT>   { t = Style.TextAlignment.AT_LEFT;   } )
	| ( <ATRIGHT>  { t = Style.TextAlignment.AT_RIGHT;  } )
	| ( <UNDER>    { t = Style.TextAlignment.UNDER;     } ) 
	| ( <ABOVE>    { t = Style.TextAlignment.ABOVE;     } ) 
	| ( <JUSTIFY>  { t = Style.TextAlignment.JUSTIFY;   } ) 
	| ( <ALONG>    { t = Style.TextAlignment.ALONG;     } ) 
	)
	{ return t; }
}

Style.IconMode iconMode():
{
	Style.IconMode i;
}
{
	( ( <NONE>    { i = Style.IconMode.NONE;     } )
	| ( <ATLEFT>  { i = Style.IconMode.AT_LEFT;  } )
	| ( <ATRIGHT> { i = Style.IconMode.AT_RIGHT; } )
	| ( <ABOVE>   { i = Style.IconMode.ABOVE;    } )
	| ( <UNDER>   { i = Style.IconMode.UNDER;    } )
	)
	{ return i; }
}

Style.VisibilityMode visMode():
{
	Style.VisibilityMode m;
}
{
	( ( <NORMAL>    { m = Style.VisibilityMode.NORMAL; } )
	| ( <HIDDEN>    { m = Style.VisibilityMode.HIDDEN; } )
	| ( <ATZOOM>    { m = Style.VisibilityMode.AT_ZOOM; } )
	| ( <UNDERZOOM> { m = Style.VisibilityMode.UNDER_ZOOM; } )
	| ( <OVERZOOM>  { m = Style.VisibilityMode.OVER_ZOOM; } )
	| ( <ZOOMRANGE> { m = Style.VisibilityMode.ZOOM_RANGE; } )
	| ( <ZOOMS>     { m = Style.VisibilityMode.ZOOMS; } )
	)
	{ return m; }
}

Style.Shape shape():
{
	Style.Shape s;
}
{
	( ( <CIRCLE>         { s = Style.Shape.CIRCLE;           } )
	| ( <BOX>            { s = Style.Shape.BOX;              } )
	| ( <ROUNDEDBOX>     { s = Style.Shape.ROUNDED_BOX;      } )
	| ( <TRIANGLE>       { s = Style.Shape.TRIANGLE;         } )
	| ( <CROSS>          { s = Style.Shape.CROSS;            } )
	| ( <DIAMOND>        { s = Style.Shape.DIAMOND;          } )
	| ( <POLYGON>        { s = Style.Shape.POLYGON;          } )
	| ( <FREEPLANE>      { s = Style.Shape.FREEPLANE;        } )
	| ( <TEXTBOX>        { s = Style.Shape.TEXT_BOX;         } )
	| ( <TEXTROUNDEDBOX> { s = Style.Shape.TEXT_ROUNDED_BOX; } )
	| ( <TEXTCIRCLE>     { s = Style.Shape.TEXT_CIRCLE;      } )
	| ( <TEXTDIAMOND>    { s = Style.Shape.TEXT_DIAMOND;     } )
	| ( <TEXTPARAGRAPH>  { s = Style.Shape.TEXT_PARAGRAPH;   } )
	| ( <JCOMPONENT>     { s = Style.Shape.JCOMPONENT;       } )

	| ( <PIECHART>       { s = Style.Shape.PIE_CHART;        } )
	| ( <FLOW>           { s = Style.Shape.FLOW;             } )
	| ( <ARROW>          { s = Style.Shape.ARROW;            } )

	| ( <LINE>           { s = Style.Shape.LINE;             } )
	| ( <ANGLE>          { s = Style.Shape.ANGLE;            } )
	| ( <CUBICCURVE>     { s = Style.Shape.CUBIC_CURVE;      } )
	| ( <POLYLINE>       { s = Style.Shape.POLYLINE;         } )
	| ( <POLYLINESCALED> { s = Style.Shape.POLYLINE_SCALED;  } )
	| ( <BLOB>           { s = Style.Shape.BLOB;             } )
	| ( <SQUARELINE>     { s = Style.Shape.SQUARELINE;       } )
	| ( <LSQUARELINE>    { s = Style.Shape.LSQUARELINE;      } )
	| ( <HSQUARELINE>    { s = Style.Shape.HSQUARELINE;      } )
	| ( <VSQUARELINE>    { s = Style.Shape.VSQUARELINE;      } )
	)
	{ return s; }
}

Style.ArrowShape arrowShape():
{
	Style.ArrowShape s;
}
{
	( ( <NONE>    { s = Style.ArrowShape.NONE;    } )
	| ( <ARROW>   { s = Style.ArrowShape.ARROW;   } )
	| ( <CIRCLE>  { s = Style.ArrowShape.CIRCLE;  } )
	| ( <DIAMOND> { s = Style.ArrowShape.DIAMOND; } )
	| ( <IMAGE>   { s = Style.ArrowShape.IMAGE;   } )
	)
	{ return s; }
}

Style.JComponents jcomponent():
{
	Style.JComponents c;
}
{
	( ( <BUTTON>    { c = Style.JComponents.BUTTON;     } )
	| ( <TEXTFIELD> { c = Style.JComponents.TEXT_FIELD; } )
	| ( <PANEL>     { c = Style.JComponents.PANEL;      } ) 
	)
	{ return c; }
}

Style.SpriteOrientation spriteOrient():
{
	Style.SpriteOrientation s;
}
{
	( ( <NONE>       { s = Style.SpriteOrientation.NONE;       } )
	| ( <TO>         { s = Style.SpriteOrientation.TO;         } )
	| ( <FROM>       { s = Style.SpriteOrientation.FROM;       } )
	| ( <NODE0>      { s = Style.SpriteOrientation.NODE0;      } )
	| ( <NODE1>      { s = Style.SpriteOrientation.NODE1;      } )
	| ( <PROJECTION> { s = Style.SpriteOrientation.PROJECTION; } )
	)
	{ return s; }
}
